import java.util.Properties

plugins {
    id("com.android.application")
    // The Flutter Gradle Plugin must be applied after the Android Gradle plugin.
    id("dev.flutter.flutter-gradle-plugin")
}

// ── Kelivo X 渠道定制（自用 fork）────────────────────────────────────────────
// 渠道由 CI 写入 android/gradle.properties 的 `x.channel=nightly|release`（缺省 = release）。
// 目的：nightly 与正式版**彼此独立包名**，可同机共存（与官方 Kelivo 也互不冲突）。
//   release → com.psyche.kelivo.x          应用名 "Kelivo X"
//   nightly → com.psyche.kelivo.x.nightly  应用名 "Kelivo X Nightly"
// ⚠️ `namespace`（Kotlin 包名/R 类）**不变**，只改 applicationId —— 故代码零改动。
val xChannel = (project.findProperty("x.channel") as String?)?.lowercase() ?: "release"
val xIsNightly = xChannel == "nightly"

android {
    namespace = "com.psyche.kelivo"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = "28.2.13676358"
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        isCoreLibraryDesugaringEnabled = true
    }

    defaultConfig {
        // Kelivo X：正式版与 nightly 各自独立包名（见上方渠道说明）
        applicationId = if (xIsNightly) "com.psyche.kelivo.x.nightly" else "com.psyche.kelivo.x"
        // 应用名按渠道生成（Manifest 用 @string/app_name 引用），随包名一起区分，避免两个图标同名难辨
        resValue("string", "app_name", if (xIsNightly) "Kelivo X Nightly" else "Kelivo X")
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        // Flutter controls APK ABI filtering, including --split-per-abi.
        externalNativeBuild {
            cmake {
                abiFilters += listOf("armeabi-v7a", "arm64-v8a", "x86_64")
            }
        }
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
        }
    }

    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    testOptions {
        unitTests.isReturnDefaultValues = true
        unitTests.isIncludeAndroidResources = true
    }

    val keystorePropertiesFile = rootProject.file("key.properties")
    val keystoreProperties = Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(keystorePropertiesFile.inputStream())
    }

    signingConfigs {
        create("release") {
            if (keystorePropertiesFile.exists()) {
                storeFile = file(keystoreProperties["storeFile"] as String)
                storePassword = keystoreProperties["storePassword"] as String
                keyAlias = keystoreProperties["keyAlias"] as String
                keyPassword = keystoreProperties["keyPassword"] as String
            }
        }
    }

    buildTypes {
        getByName("release") {
            if (keystorePropertiesFile.exists()) {
                signingConfig = signingConfigs.getByName("release")
            }
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_11
    }
}

flutter {
    source = "../.."
}

val requiredProotLibs = listOf(
    "armeabi-v7a/libproot_exec.so",
    "armeabi-v7a/libproot_loader.so",
    "armeabi-v7a/libtalloc.so",
    "armeabi-v7a/libandroid-shmem.so",
    "arm64-v8a/libproot_exec.so",
    "arm64-v8a/libproot_loader.so",
    "arm64-v8a/libtalloc.so",
    "arm64-v8a/libandroid-shmem.so",
    "x86_64/libproot_exec.so",
    "x86_64/libproot_loader.so",
    "x86_64/libtalloc.so",
    "x86_64/libandroid-shmem.so",
)

tasks.register<Exec>("fetchProot") {
    val repoRoot = rootProject.projectDir.parentFile
    commandLine("bash", repoRoot.resolve("tool/fetch_proot.sh").absolutePath)
    workingDir = repoRoot
    onlyIf {
        val jniLibs = layout.projectDirectory.dir("src/main/jniLibs")
        requiredProotLibs.any { name ->
            val so = jniLibs.file(name).asFile
            !so.isFile || so.length() == 0L
        }
    }
}

tasks.whenTaskAdded {
    if (name == "preBuild") {
        dependsOn("fetchProot")
    }
}
tasks.findByName("preBuild")?.dependsOn("fetchProot")

dependencies {
    implementation("androidx.browser:browser:1.9.0")
    implementation("org.tukaani:xz:1.10")
    // Required for core library desugaring (used by flutter_local_notifications)
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.4")
    testImplementation("junit:junit:4.13.2")
    testImplementation("org.robolectric:robolectric:4.16.1")
}
